local CreateFader = function()
{
	local Fader = {};
	Fader.threadId = null;

	Fader.screenCam2d = Cam2d();
	Fader.screenCam2d.InitScreenSpace();

	Fader.Init = function()
	{
		.threadId = null;
	};

	Fader.KillFade = function()
	{
		if ( ?.threadId )
		{
			if ( threadIsAlive(.threadId ))
			{
				threadKill(.threadId);
			}
			.threadId = null;
		}
	};

	Fader.Flash = function( secondsIn, secondsOut, color, callbackFinishFunc = null, callbackObj = null, callbackArgs = null )
	{
		Log("Flash..");
		assert( secondsIn >= 0.0f && secondsOut >= 0.0f && ?color );

		.KillFade();

		fork id
		{
			threadSetGroup( ThreadGroups.MAIN );

			local data = 
			{
				fadeCoeff = 0.0f,
				color = color
			};

			// Fade in
			while( data.fadeCoeff < 1.0f)
			{
				data.fadeCoeff += g_dt	/ secondsIn;
				RegisterDraw( DrawLayers.ScreenFade, .Draw, this, data );

				yield();
			}
			data.fadeCoeff = 1.0f;

			// Fade out
			while( data.fadeCoeff > 0.0f)
			{
				data.fadeCoeff -= g_dt	/ secondsOut;
				RegisterDraw( DrawLayers.ScreenFade, .Draw, this, data );

				yield();
			}

			// callback object
			if ( ?callbackFinishFunc )	{ callbackObj:callbackFinishFunc(callbackArgs); }

			.KillFade();
		};

		
		.threadId = id;
	};

	Fader.FadeIn = function( seconds, color, callbackFinishFunc = null, callbackObj = null, callbackArgs = null )
	{
		Log("Fade In..");

		assert( seconds >= 0.0f && ?color );

		.KillFade();

		fork id
		{
			threadSetGroup( ThreadGroups.MAIN );

			local data = 
			{
				fadeCoeff = 1.0f,
				color = color
			};

			while( data.fadeCoeff > 0.0f)
			{
				data.fadeCoeff -= g_dt	/ seconds;
				RegisterDraw( DrawLayers.ScreenFade, .Draw, this, data );

				yield();
			}

			// callback object
			if ( ?callbackFinishFunc )	{ callbackObj:callbackFinishFunc(callbackArgs); }

			.KillFade();
		};

		.threadId = id;
	};

	Fader.FadeOut = function( seconds, color, callbackFinishFunc = null, callbackObj = null, callbackArgs = null, extra_wait = null )
	{
		Log("Fade Out..");
		assert( seconds >= 0.0f && ?color );

		.KillFade();

		fork id
		{
			threadSetGroup( ThreadGroups.MAIN );

			local data = 
			{
				fadeCoeff = 0.0f,
				color = color
			};

			while( data.fadeCoeff < 1.0f)
			{
				data.fadeCoeff += g_dt	/ seconds;
				RegisterDraw( DrawLayers.ScreenFade, .Draw, this, data );

				yield();
			}

			// callback object
			if ( ?callbackFinishFunc )	{ callbackObj:callbackFinishFunc(callbackArgs); 	}

            local frames = 0;
            if (?extra_wait)
            {
                frames = (extra_wait * 60.0f).Int();
            }

            for (i = 0; i < frames; i += 1)
            {
    			RegisterDraw( DrawLayers.ScreenFade, .Draw, this, data );
                yield();
            }

			// Register at least one more call or else the game will flicker one frame
			RegisterDraw( DrawLayers.ScreenFade, .Draw, this, data );

			.KillFade();
		};

		.threadId = id;
	};

	Fader.WaitOn = function()
	{
		while (?.threadId && threadIsAlive(.threadId))
		{
			yield();
		}
	};

	Fader.Draw = function( data )
	{
		Gfx.Enable(Gfx.BLEND);

		.screenCam2d.Begin();
		Gfx.BeginDefaultShader();

		Gfx.Color( data.color, data.fadeCoeff );
		Gfx.DrawRect(v2(0), v2(g_core.screenDimen) );

		Gfx.EndDefaultShader();
		.screenCam2d.End();
	};

	return Fader;
};

global g_fader = CreateFader();
g_fader.Init();