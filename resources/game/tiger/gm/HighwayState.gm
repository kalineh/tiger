global MakeHighwayState = function()
{
	local State = 
	{
	};

	State.Update = function()
	{
		while( true )
		{		
            local scroll = v2(5.0f, 0.0f);
    		.cam.SetTargetPos(.cam.targetPos + scroll);

			RegisterGui(.Gui, this);
			yield();
		}
	};

	State.Gui = function()
	{
		Gui.Begin("Highway State", g_core.screenDimen.x*0.4f, g_core.screenDimen.y-10.0f );
		
		Gui.End();
	};

	State._Init = function()
	{	
		.bg = MakeBackground("Highway");

		.cam = MakeGameCamera();
		.world = MakeWorld("Highway");
		.world.Init(true);
        .spawner = MakeCarSpawner(.world);
		.gui = MakeGameGui();

		._InitTextures();
		._InitParticles();

		.threadId = this:thread(.Update);
		.finish_thread = this:thread(.Finish);
	};

    State.Finish = function()
    {
        sleep(60.0f * 1.0f);
		g_game.musicPlayer.FadeOut(2.0f);
		g_fader.FadeOut(3.0f, Gfx.BLACK, null, null, null, 3.0f);

        g_game.voicePlayer.Play("highway_finished_0");

        // HACK
        .world.player.OnCollide = function() { };
        .world.player.OnPreUpdate = function() { };
        .world.player.OnPostUpdate = function() { };

        foreach (e in .world.entities)
        {
            if (e.type == "Car")
            {
                e.Die();
            }
        }

        .spawner.Release();

        local speedaway = 0.0f;
        for (i = 0; i < 120; i += 1)
        {
            speedaway += 0.2f;
            .world.player.pos += v3(speedaway, 0.0f, 0.0f);
            yield();
        }

        g_game.voicePlayer.Play("highway_finished_1");

		g_fader.WaitOn();
        g_game.GotoState("game");
    };

	State.EmitParticlesAt = function(particleName, pos, numParticles)
	{
		local particles = .particles[particleName];
		assert(?particles);
		particles.cpp.pos = pos;
		particles.cpp.Emit(numParticles);
	};

	State._InitParticles = function()
	{
		local particlesData =
		{
			smoke = { numParticles=1024, drawLayer=DrawLayers.Scene, file="TIGER_smoke.gm" },
			blood = { numParticles=1024, file="TIGER_blood.gm" },
			fire = { numParticles=4096, drawLayer=DrawLayers.Scene, file="TIGER_fire.gm" },
		};
		
		.particles = {};

		foreach( name and data in particlesData )
		{
			local particles = MakeParticles2d(data.numParticles);
			particles.LoadFile(data.file);
			particles.drawLayer = select( ?data.drawLayer, data.drawLayer, DrawLayers.Scene_FG);
			particles.useScreenCamera = select(?data.useScreenCamera, data.useScreenCamera, false );

			.particles[name] = particles;
		}
	};

	State._InitTextures = function()
	{
		.textures = 
		{
			//projectileRocket = Texture("game/tiger/img/projectileRocket.png"),
		};
	};

	State.Release = function()
	{
		.bg.Release();
		.bg = null;

        .spawner.Release();
        .spawner = null;

		.world.Release();
		.world = null;

		.gui.Release();
		.gui = null;

		.texBG = null;
		.textures = null;
		
		.cam.Release();
		.cam = null;

		threadKill(.threadId);
	};
	
	State._Init();
	return State;
};
