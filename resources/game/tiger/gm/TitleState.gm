global MakeTitleState = function()
{
	local State = 
	{
		transitionThread = null,
	};

	State.Draw = function()
	{
		Gfx.BeginDefaultShader();

		local posOffset = v2(0.0f, 10.0f*sin(g_game.time*2.0f) );

		// title
		local texDimen = .texTitle.Dimen()*0.5f;
		.texTitle.Bind(0);
		Gfx.DrawRectTexCoords( posOffset+g_game.dimen*0.5f-texDimen*0.5f + v2(0.0f,60.0f), texDimen);
		.texTitle.Unbind();

		Gfx.EndDefaultShader();
	};

	State.Update = function()
	{
		g_game.musicPlayer.Play(g_game.songs.title, 0.1f);

		while( true )
		{		
			if (!?.transitionThread)
			{
				if (Input.DidMouseJustGoDown(0) ||
					Input.DidMouseJustGoDown(1) ||
					Input.DidKeyJustGoDown("SPACE") ||
					Input.DidKeyJustGoDown("RETURN") ||
					Input.DidKeyJustGoDown("KP_ENTER"))
				{
					fork id
					{
						g_game.musicPlayer.FadeOut(1.0f);
						g_game.soundBanks["shared"].Play("selectCard"); // TODO: specific sound
						local fadeTime = 1.0f;
						g_fader.FadeOut(fadeTime, Gfx.WHITE);
						g_fader.WaitOn();
						g_game.GotoState("game");
					}
					.transitionThread = id;
				}
			}

			yield();
		}
	};

	State.DrawBegin = function()
	{
		g_game.BeginScreenSpace();
	};

	State.DrawEnd = function()
	{
		g_game.EndScreenSpace();
	};

	State.DrawBG = function()
	{
		Gfx.BeginDefaultShader();
		Gfx.Color(Gfx.WHITE);

		// background img
		.texBG.Bind(0);
		Gfx.DrawRectTexCoords(v2(0.0f), .texBG.Dimen());
		.texBG.Unbind();

		Gfx.EndDefaultShader();
	};

	State.Init = function()
	{	
		.threadId = this:thread(.Update);

		.bokeh = MakeCrewSelectBokeh();

		.texTitle = Texture("game/tiger/img/title/logo.png");
		.texBG = Texture("game/tiger/img/crewSelect/bg.png");
	};

	State.Release = function()
	{
		.texBG = null;

		.bokeh.Release();
		.bokeh = null;

		threadKill(.threadId);
	};
	
	State.Init();
	return State;
};
